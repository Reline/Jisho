import groovy.json.JsonSlurper

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
//    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.kapt'
}

sourceCompatibility = 1.8

jar {
    manifest {
        attributes 'Main-Class': 'com.github.reline.jisho.JishoDBKt'
    }
}

configurations {
    compile.exclude group: 'stax'
    compile.exclude group: 'xpp3'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KaptGenerateStubs).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(':common')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    implementation 'org.xerial:sqlite-jdbc:3.21.0.1'
    implementation "app.cash.sqldelight:sqlite-driver:$sqldelight_version"

    implementation "com.tickaroo.tikxml:core:$tikxml_version"
    implementation ("com.tickaroo.tikxml:annotation:$tikxml_version")
    kapt "com.tickaroo.tikxml:processor-common:$tikxml_version"
    kapt "com.tickaroo.tikxml:processor:$tikxml_version"

    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.okio:okio:$okio_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

final outputDir = new File("$buildDir/dict")
outputDir.mkdirs()

task fetchFuriganaDictionaries {
    doLast {
        // https://docs.github.com/en/rest/reference/repos#get-a-release-by-tag-name
        println("Downloading furigana sources...")
        final String url = "https://api.github.com/repos/Doublevil/JmdictFurigana/releases/tags/$jmdictfurigana_version"
        final json = new JsonSlurper().parseText(url.toURL().text)
        println("Parsing assets from $url...")
        final assets = json['assets'] as List<Object>
        final downloads = new HashMap<String, String>()
        println("Found ${assets.size()} assets")
        for (asset in assets) {
            final String name = asset['name']
            if (name.endsWith(".json")) {
                downloads.put(name, asset["browser_download_url"])
            }
        }
        println("Found ${downloads.size()} available downloads")
        for (download in downloads) {
            println(download)
            final downloadUrl = new URL(download.value)
            println("Download url: ${downloadUrl}")
            final dictionary = new File("$outputDir/${download.key}")
            println("Download destination: ${dictionary.absolutePath}")
            if (dictionary.exists()) {
                println("${dictionary.name} already exists, skipping")
                continue
            }

            try {
                println("Creating file at ${dictionary.absolutePath}...")
                if (!dictionary.createNewFile()) {
                    // todo: change to warning, possibly error?
                    println("[WARN]: Could not create file at ${dictionary.absolutePath}")
                    continue
                }
            } catch (e) {
                println(e)
                continue;
            }

            println("Created file at ${dictionary.absolutePath}")
            dictionary.withOutputStream { out ->
                println("Downloading ${download.key}...")
                downloadUrl.withInputStream { from ->  out << from }
            }
            println("Downloaded ${download.value} to ${dictionary.absolutePath}")
        }
    }
}

task prepareMonashDictionaries {
    doLast {
        println("Inflating dictionary sources...") // http://ftp.monash.edu/pub/nihongo/

        def dictResourcesDir = new File("$projectDir/src/main/res/dict")

        def dictZips = dictResourcesDir.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return name.endsWith(".zip")
            }
        })
        for (zip in dictZips) {
            println(zip.absolutePath)
            copy {
                from zipTree(zip)
                into outputDir
            }
        }

        def dictGunZips = dictResourcesDir.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return name.endsWith(".gz")
            }
        })
        for (gunZip in dictGunZips) {
            println(gunZip.absolutePath)
            def filename = gunZip.name
            def basename = filename.take(filename.lastIndexOf('.'))
            def file = new File("${outputDir.absolutePath}/${basename}")
            file.withOutputStream { os ->
                os << resources.gzip(gunZip.path).read()
            }
        }
        println("Converting files to UTF-8 format...")
        // EUC-JP [csEUCPkdFmtjapanese, x-euc-jp, eucjis, Extended_UNIX_Code_Packed_Format_for_Japanese, euc_jp, eucjp, x-eucjp] to UTF-8
        for (dictionary in outputDir.listFiles()) {
            // we don't care about the intro files
            if (dictionary.name.contains("intro")) {
                dictionary.delete()
                continue
            }

            // skip xml & json files
            if (dictionary.name.endsWith(".xml") || dictionary.name.endsWith(".json")) {
                continue
            }

            println(dictionary.absolutePath)
            def temp = new File("${outputDir.absolutePath}/temp_${dictionary.name}")
            new FileOutputStream(temp).withWriter("UTF-8") { writer ->
                new FileInputStream(dictionary).withReader("EUC-JP") { reader ->
                    writer << reader
                }
            }
            dictionary.delete()
            temp.renameTo(dictionary)
        }
    }
}

task prepareDictionaries(dependsOn: ['fetchFuriganaDictionaries', 'prepareMonashDictionaries']) {}
// prepare dictionaries after building
assemble.finalizedBy(prepareDictionaries)
